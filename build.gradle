/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    // Define versions in a single place
    ext {
        // Sdk and tools
        compileSdkVersion = 30
        minSdkVersion = 21
        targetSdkVersion = 30

        // App dependencies
        accompanistVersion = '0.3.3.compose-6953474-SNAPSHOT'
        appCompatVersion = '1.1.0'
        composeVersion = '1.0.0-SNAPSHOT'
        constraintLayoutVersion = '2.0.4'
        coreTestingVersion = '2.1.0'
        coroutinesVersion = "1.3.9"
        espressoVersion = '3.3.0'
        fragmentVersion = '1.1.0-alpha09'
        glideVersion = '4.10.0'
        gradleVersion = '4.2.0-alpha16'
        gsonVersion = '2.8.6'
        junitVersion = '4.13'
        kotlinVersion = '1.4.10'
        ktlintVersion = '0.39.0'
        ktxVersion = '1.5.0-alpha02'
        lifecycleVersion = '2.2.0-alpha01'
        materialVersion = '1.2.1'
        navigationVersion = '2.3.1'
        recyclerViewVersion = '1.1.0-alpha05'
        roomVersion = '2.2.5'
        runnerVersion = '1.0.1'
        truthVersion = '1.0.1'
        testExtJunit = '1.1.0'
        uiAutomatorVersion = '2.2.0'
        viewPagerVersion = '1.0.0'
        workVersion = '2.4.0'
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
    }
}

plugins {
    id "com.diffplug.spotless" version "5.1.1"
}

allprojects {
    repositories {
        maven { // Compose alpha07 snapshot
            url "https://androidx.dev/snapshots/builds/6953474/artifacts/repository/"
        }

        google()
        jcenter()
        mavenCentral()

        // Accompanist snapshots
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        // Compose's MDC Theme adapter snapshots
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group.startsWith('androidx.compose')
                    || details.requested.group.startsWith('androidx.ui')) {
                details.useVersion project.composeVersion
            }
        }
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        targetExclude("$buildDir/**/*.kt")
        targetExclude('bin/**/*.kt')
        ktlint(ktlintVersion)
    }
}
