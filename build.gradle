/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    // Define versions in a single place
    ext {
        // Sdk and tools
        compileSdkVersion = 30
        minSdkVersion = 21
        targetSdkVersion = 30

        // App dependencies
        accompanistVersion = '0.16.0'
        activityComposeVersion = '1.3.1'
        appCompatVersion = '1.3.1'
        coilVersion = '1.3.2'
        composeVersion = '1.0.1'
        constraintLayoutVersion = '2.1.0'
        constraintLayoutComposeVersion = '1.0.0-beta02'
        coreTestingVersion = '2.1.0'
        coroutinesVersion = "1.5.1"
        espressoVersion = '3.4.0'
        fragmentVersion = '1.3.6'
        glideVersion = '4.12.0'
        gradleVersion = '7.0.0'
        gsonVersion = '2.8.6'
        junitVersion = '4.13.2'
        kotlinVersion = '1.5.21'
        ktlintVersion = '0.40.0'
        ktxVersion = '1.6.0'
        lifecycleVersion = '2.3.1'
        materialVersion = '1.4.0'
        navigationVersion = '2.3.5'
        recyclerViewVersion = '1.2.1'
        roomVersion = '2.3.0'
        runnerVersion = '1.0.1'
        truthVersion = '1.1.3'
        testExtJunit = '1.1.3'
        uiAutomatorVersion = '2.2.0'
        viewModelComposeVersion = '1.0.0-alpha07'
        viewPagerVersion = '1.0.0'
        workVersion = '2.5.0'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
    }
}

plugins {
    id "com.diffplug.spotless" version "5.1.1"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        targetExclude("$buildDir/**/*.kt")
        targetExclude('bin/**/*.kt')
        ktlint(ktlintVersion)
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            // Use experimental APIs
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
        }
    }
}
