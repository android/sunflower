/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    // Define versions in a single place
    ext {
        // Sdk and tools
        compileSdkVersion = 31
        minSdkVersion = 21
        targetSdkVersion = 31

        // App dependencies
        appCompatVersion = '1.4.1'
        constraintLayoutVersion = '2.1.3'
        coreTestingVersion = '2.1.0'
        coroutinesVersion = "1.6.1"
        espressoVersion = '3.4.0'
        fragmentVersion = '1.4.1'
        glideVersion = '4.13.1'
        gradleVersion = '7.1.2'
        gsonVersion = '2.9.0'
        hiltVersion = '2.41'
        junitVersion = '4.13.2'
        kotlinVersion = '1.6.20'
        ktlintVersion = '0.40.0'
        ktxVersion = '1.7.0'
        lifecycleVersion = '2.4.1'
        materialVersion = '1.5.0'
        navigationVersion = '2.4.1'
        okhttpLoggingVersion = '4.9.3'
        pagingVersion = '3.1.1'
        retrofitVersion = '2.9.0'
        roomVersion = '2.4.2'
        runnerVersion = '1.0.1'
        truthVersion = '1.1.3'
        testExtJunit = '1.1.3'
        uiAutomatorVersion = '2.2.0'
        viewPagerVersion = '1.0.0'
        workVersion = '2.7.1'
        // profileInstallerVersion = '1.2.0-alpha02'
        profileInstallerVersion = '1.2.0-SNAPSHOT'
        uiAutomatorVersion = '2.2.0'
        // benchmarkVersion = '1.1.0-beta06'
        benchmarkVersion = '1.2.0-SNAPSHOT'
        tracingVersion = '1.1.0-SNAPSHOT'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
    }
}

plugins {
    id "com.diffplug.spotless" version "6.4.1"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://androidx.dev/snapshots/builds/8442307/artifacts/repository' }
    }

    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'androidx.tracing' && details.requested.name == 'tracing-ktx') {
                details.useVersion "1.1.0-SNAPSHOT"
            }

            if (details.requested.group == 'androidx.profileinstaller') {
                details.useVersion "1.2.0-SNAPSHOT"
            }
        }
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        ktlint(ktlintVersion).userData(['max_line_length' : '100'])
    }
}
