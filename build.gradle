/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    // Define versions in a single place
    ext {
        // Sdk and tools
        compileSdkVersion = 28
        minSdkVersion = 21
        targetSdkVersion = 28
        composeSnapshot = '6860046'

        // App dependencies
        accompanistVersion = "0.2.3.compose-6860046-SNAPSHOT"
        appCompatVersion = '1.1.0'
        composeVersion = '1.0.0-SNAPSHOT'
        constraintLayoutVersion = '2.0.0-beta3'
        coreTestingVersion = '2.1.0'
        coroutinesVersion = "1.3.9"
        espressoVersion = '3.2.0'
        fragmentVersion = '1.1.0-alpha09'
        glideVersion = '4.10.0'
        gradleVersion = '4.2.0-alpha12'
        gsonVersion = '2.8.2'
        junitVersion = '4.13'
        kotlinVersion = '1.4.10'
        ktlintVersion = '0.39.0'
        ktxVersion = '1.5.0-alpha02'
        lifecycleVersion = '2.2.0-alpha01'
        materialVersion = '1.1.0-alpha09'
        navigationVersion = '2.2.0'
        recyclerViewVersion = '1.1.0-alpha05'
        roomVersion = '2.1.0'
        runnerVersion = '1.0.1'
        truthVersion = '0.42'
        testExtJunit = '1.1.0'
        uiAutomatorVersion = '2.2.0'
        viewPagerVersion = '1.0.0'
        workVersion = '2.1.0'
    }

    repositories {
        // Compose snapshots repo
        maven { url "https://androidx.dev/snapshots/builds/$composeSnapshot/artifacts/ui/repository/" }
        // Accompanist snapshots repo
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
    }
}

plugins {
    id "com.diffplug.spotless" version "5.1.1"
}

allprojects {
    repositories {
        // Compose snapshots repo
        maven { url "https://androidx.dev/snapshots/builds/$composeSnapshot/artifacts/ui/repository/" }
        // Accompanist snapshots repo
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

        google()
        jcenter()
        mavenCentral()
    }

    // Create a configuration which allows us to intercept the JARs, and add them to
    // Kotlin Compiler freeCompilerArgs. This is needed because we can't currently use the
    // built-in `compose` feature in AGP, since it depends on the old Compose Compiler artifacts.
    // TODO: Remove this once AGP uses the new Compose Compiler artifact name
    def compilerPluginConfig = project.configurations.create("kotlinCompilerPlugin")

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach { compile ->
        compile.dependsOn(compilerPluginConfig)
        compile.doFirst {
            if (!compilerPluginConfig.isEmpty()) {
                // Add the compiler plugin JARs using the -Xplugin flag
                compile.kotlinOptions.freeCompilerArgs +=
                        "-Xplugin=${compilerPluginConfig.files.first()}"
                // Need to turn on the IR compiler too
                compile.kotlinOptions.useIR = true
            }
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group.startsWith('androidx.compose')
                || details.requested.group.startsWith('androidx.ui')) {
            details.useVersion "1.0.0-SNAPSHOT"
        }
    }
}


spotless {
    kotlin {
        target "**/*.kt"
        targetExclude("$buildDir/**/*.kt")
        targetExclude('bin/**/*.kt')
        ktlint(ktlintVersion)
    }
}
